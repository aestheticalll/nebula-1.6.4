plugins {
    id 'java'
    id 'de.fuerstenau.buildconfig' version '1.1.4'
}

group = 'yzy.szn'
archivesBaseName = "Redline+${project.build_number}"

def execute(String cmd) {
    try {
        def stdout = new ByteArrayOutputStream()

        exec {
            commandLine cmd.split(" ")
            standardOutput = stdout
        }

        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}

buildConfig {
    buildConfigField "String", "HASH", "${execute("git rev-parse --short HEAD")}"
    buildConfigField "String", "BRANCH", "${execute("git branch").replace("* ", "")}"
    buildConfigField "String", "VERSION", "${project.version}"
    buildConfigField "int", "BUILD_NUMBER", "${project.build_number}"

    packageName "yzy.szn.launcher"
}

sourceSets.main.java.srcDirs += "build/gen/buildconfig/src/main"

sourceCompatibility = targetCompatibility = '8'
compileJava {
    sourceCompatibility = targetCompatibility = '8'
    options.encoding 'UTF-8'
}

repositories {
    mavenCentral()
}

configurations {
    dependency
    implementation.extendsFrom dependency
}

dependencies {

    implementation fileTree('libraries')
    implementation configurations.dependency
}

jar {
    from(configurations.dependency.collect { it.isDirectory() ? it : zipTree(it) })

    manifest.attributes(
            'Main-Class': 'Start',
            'Class-Path': '.'
    )
}
